
' '<Summary> An internal interface exposed by VBA for all components (modules, class modules, etc)
'     '<remarks> This internal interface is known to be supported since the very earliest version of VBA6
'     '[ComImport(), Guid("DDD557E1-D96F-11CD-9570-00AA0051E5D4")]
'     '[InterfaceType(ComInterfaceType.InterfaceIsIUnknown)]
'     'Public Enum IVBEComponentVTableOffsets           '+3 for the IUnknown
'     '    CompileComponentOffset = 12 + 3              'void CompileComponent();
'     '    GetStdModAccessorOffset = 14 + 3             'IDispatch GetStdModAccessor();
'     '    GetSomeRelatedTypeInfoPtrsOffset = 34 + 3    'void GetSomeRelatedTypeInfoPtrs(out IntPtr a, out IntPtr b);        // returns 2 TypeInfos, seemingly related to this ITypeInfo, but slightly different.
'     'End Enum

[ InterfaceId ("DDD557E1-D96F-11CD-9570-00AA0051E5D4") ]
Interface IVBEComponent Extends IUnknown
	Sub Placeholder01()
    Sub Placeholder02()
    Sub Placeholder03()
    Sub Placeholder04()
    Sub Placeholder05()
    Sub Placeholder06()
    Sub Placeholder07()
    Sub Placeholder08()
    Sub Placeholder09()
    Sub Placeholder10()
    Sub Placeholder11()
    Sub Placeholder12()
    '[ PreserveSig ] I'm sceptical
    Sub CompileComponent()
    Sub Placeholder13()
    'apparently it's HRESULT & RD is a bit misleading, so no PreserveSig here
    Function GetStdModAccessor() As Object
    'Placeholder(1 To 19) As LongPtr
    'GetSomeRelatedTypeInfoPtrs As LongPtr
End Interface

Private Class VBETypeLibIterator
	Private Type TIterator
        currentTL As VBETypeLibObj
        pCurrentTL As LongPtr
    End Type

    Private this As TIterator

    Public Property Let baseTypeLib(ByVal rawptr As LongPtr)
        currentTL = rawptr
        ResetIteration
    End Property

    Private Property Let currentTL(ByVal rawptr As LongPtr)
        this.pCurrentTL = rawptr
        CopyMemory this.currentTL, ByVal rawptr, LenB(this.currentTL)
    End Property

    Public Sub ResetIteration()
        Logger.Log TraceLevel, "Resetting iteration of typelib iterator"
        Logger.Log DebugLevel, printf("Before Reset: {}<-{}->{}", this.currentTL.Prev, this.pCurrentTL, this.currentTL.Next)
        Do While this.currentTL.Prev <> 0
            currentTL = this.currentTL.Prev
            Logger.Log DebugLevel, printf("Resetting to: {}<-{}->{}", this.currentTL.Prev, this.pCurrentTL, this.currentTL.Next)
        Loop
        Logger.Log TraceLevel, "Iterator Reset Complete"
    End Sub

    Private Sub Yield()
        'The first yield will be right after a reset
        ' therefore always dereference the currentTL before incrementing
        ' otherwise we'll never return the first one
        If this.pCurrentTL = 0 Then Err.Raise 5, Description:="We've reached the end of the line"
        currentTL = this.pCurrentTL 'dereference to fill struct. Technically not necessary on first iteration
        Logger.Log DebugLevel, printf("After Move: {}<-{}->{}", this.currentTL.Prev, this.pCurrentTL, this.currentTL.Next)
        
        'Set up the next yield...
        'We will pick this up as zero next time around
        ' if we are at the end of the list already
        this.pCurrentTL = this.currentTL.Next
    End Sub

    [ Description ("Gets type library com objects from list") ]
    Public Function TryGetNext(ByRef outTypeLib As TypeLibInfo) As Boolean
        On Error GoTo cleanFail
        Dim tlPtr As LongPtr = this.pCurrentTL
        Yield
        Set outTypeLib = New TypeLibInfo(tlPtr) 'no worries about query interface for ITypeLib
        Return True

    cleanFail:
        Set outTypeLib = Nothing
        Return False
    End Function
End Class

Module VBETypeLib
	Private Type VBEReferencesObj
        vTable1 As LongPtr                           'To _References vtable
        vTable2 As LongPtr
        vTable3 As LongPtr
        object1 As LongPtr
        object2 As LongPtr
        typeLib As LongPtr
        placeholder1 As LongPtr
        placeholder2 As LongPtr
        RefCount As LongPtr
    End Type

    Public Type VBETypeLibObj
        vTable1 As LongPtr                           'To ITypeLib vtable
        vTable2 As LongPtr
        vTable3 As LongPtr
        Prev As LongPtr
        '@Ignore KeywordsUsedAsMember: makes it clearer despite what RD wants
        Next As LongPtr
    End Type
      
    
    Public Function StdModuleAccessor(ByVal moduleName As String, ByVal vbProj As VBProject, ByVal projectName As String, Optional ByRef outModuleTypeInfo As ITypeInfo, Optional ByRef outTypeLib As TypeLibInfo) As Object
        Logger.Log TraceLevel, "Get ref instance ptr"
        Dim referencesInstancePtr As LongPtr
        referencesInstancePtr = ObjPtr(vbProj.References)
        Assert referencesInstancePtr <> 0, "referencesInstancePtr = 0"
        
        Logger.Log TraceLevel, "Get ref obj"
        'The references object instance looks like this, and has a raw pointer contained within it to the typelibs it uses
        Dim refData As VBEReferencesObj
        CopyMemory refData, ByVal referencesInstancePtr, LenB(refData)
        Assert refData.vTable1 = MemLongPtr(referencesInstancePtr), "refData.vTable1 <> MemLongPtr(referencesInstancePtr)"
        
        Logger.Log TraceLevel, "Get typelib instance table"
        Dim typeLibInstanceTable As VBETypeLibObj
        CopyMemory typeLibInstanceTable, ByVal refData.typeLib, LenB(typeLibInstanceTable)

        'Create a class to iterate over the doubly linked list
        Dim typeLibPtrs As New VBETypeLibIterator
        typeLibPtrs.baseTypeLib = refData.typeLib
        
        'Now we could use proj.module.sub to find something in particular
        'For now though, we just want a reference to the typeInfo for the ExampleModule
        Dim found As Boolean

        Logger.Log TraceLevel, "Try iterate over typelib"
        Do While typeLibPtrs.TryGetNext(outTypeLib)
            Dim tlName As String = outTypeLib.name
            If tlName = "" Then
                tlName = "[Immediate]"
                LogManager.Log DebugLevel, printf("outTypeLib.name was empty, continuing assuming it was immediate window")
            End If
            LogManager.Log InfoLevel, printf("Discovered Project: '{}'@{}", tlName, ObjPtr(outTypeLib.ITypeLib))
                  
            If tlName = projectName Then
                'we have found the project typelib, check for the correct module within it
                Dim moduleTI As ITypeInfo
                If outTypeLib.tryGetTypeInfoByName(moduleName, outTI:=moduleTI) Then
                    Logger.Log DebugLevel, "Got a typeinfo @" & ObjPtr(moduleTI)
                    found = True
                    Exit Do
                Else
                    Err.Raise vbObjectError + 5, Description:=printf("Module with name '{}' not found in project '{}'", moduleName, projectName)
                End If
            End If
        Loop
        If Not found Then Err.Raise vbObjectError + 5, Description:=printf("No project found named '{}'", projectName)

        'Cast to IVBEComponent Guid("DDD557E1-D96F-11CD-9570-00AA0051E5D4")
        '   In RD this is done via Aggregation
        '   Meaning an object is made by merging the COM interface with a managed C# interface
        '   We don't have to worry about this, it is just to avoid some bug with C# reflection I think
        
        Logger.Log DebugLevel, "Trying to cast object @" & ObjPtr(moduleTI)
        Dim vbeComponent As IVBEComponent
        Set vbeComponent = moduleTI
        Logger.Log DebugLevel, "Got an IVBEComponent @" & ObjPtr(vbeComponent)
        
        Set outModuleTypeInfo = moduleTI
        Set StdModuleAccessor = vbeComponent.GetStdModAccessor()

    End Function

End Module